plugins {
    id 'org.springframework.boot' version '2.6.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.xlffm3'
version = '1.0.0'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.xlffm3'
            artifactId = 'query-counter'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'query-counter'
                description = 'A library for counting queries for Spring Data JPA application'
                url = 'https://github.com/xlffm3/query-counter'

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/JavaUnit/AutoParams/blob/main/LICENSE"
                    }
                }

                developers {
                    developer {
                        id = 'xlffm3'
                        name = 'Jinhong Park'
                        email = 'xlffm3@gmail.com'
                    }
                }

                scm {
                    connection = "https://github.com/xlffm3/query-counter.git"
                    developerConnection = "https://github.com/xlffm3/query-counter.git"
                    url = "https://github.com/xlffm3/query-counter"
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username project.ossrhUsername
                password project.ossrhPassword
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
}
